#!/usr/bin/env coffee
_ = require 'pg-promise'
glob = require "glob"

query = (e)->console.log e.query
pgp = _ {query}

argParser = require './src/arg-parser'
{dbname, srid, schema, tiles} = argParser()

console.log(dbname, srid, schema)

{QueryFile} = pgp
{readFileSync} = require 'fs'

connection = null
if dbname? and dbname.startsWith("postgresql://")
  connection = dbname

if not connection?
  connection = "postgresql:///#{dbname}"

db = pgp(connection)

params = {
  schema
  # `data_schema` is for interoperability with PostGIS-Geologic-Map
  data_schema: schema
  srid
}

createFixtures = ->
  for file in glob.sync("#{__dirname}/db-fixtures/*.sql")
    sql = QueryFile file, {params}
    await db.query sql

do ->
  try
    await db.query "SELECT count(*) FROM ${schema~}.linework", {schema}
    process.exit()
  catch err
    console.log("Tables not found, creating...")

  try
    await createFixtures()
  catch err
    console.error err

  process.exit()
